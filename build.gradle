plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'info.solidsoft.pitest' version '1.15.0'
}

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
}

dependencies {

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    compileOnly 'org.slf4j:slf4j-api:2.0.5'

    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.slf4j:slf4j-api:2.0.5'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        xml.destination file("$buildDir/jacoco/test.xml")
        html.destination file("$buildDir/jacoco/index.html")
        html.required = true
        csv.required = true
    }


}

version = '1.0.0'

publishing {

    publications {
        maven(MavenPublication) {
            from components.java
            groupId 'com.linbrox.lang'
            artifactId 'lib-zepolar-lang'
            version= project.version
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/zepolar/zepolar-lang"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

jar {

    into("META-INF/maven/$project.group/$project.name") {
        from generatePomFileForMavenPublication
        rename ".*", "pom.xml"
    }
}

pitest {
    junit5PluginVersion = '1.0.0'    //or 0.15 for PIT <1.9.0
    targetClasses = ['com.linbrox.common.*']  // Adjust the package to match where your classes are
    targetTests = ['com.linbrox.common.*']    // Adjust the package to match where your tests are
    threads = 4                            // Number of threads to use
    outputFormats = ['HTML']               // Optional: Specify output formats
    timestampedReports = false             // Optional: Disable timestamped reports
    mutationThreshold = 80
}

